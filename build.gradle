plugins {
    id "groovy"
    id "io.github.gradle-nexus.publish-plugin"
    id "java-library"
    id "maven-publish"
    id "signing"
}

repositories {
    mavenCentral()
}

def isRelease = providers.gradleProperty("release").orNull == "true"

group = 'com.gradle'
def nextVersion = '0.5'
version = isRelease ? nextVersion : "${nextVersion}-dev"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    testImplementation 'org.apache.groovy:groovy-all:4.0.24'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    testImplementation 'org.spockframework:spock-junit4:2.3-groovy-4.0'
    testImplementation 'junit:junit:4.13.2'
}

test {
    useJUnitPlatform()
}

nexusPublishing {
    repositories {
        // see https://central.sonatype.org/publish/publish-portal-ossrh-staging-api/#configuration
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = 'ansi-control-sequence-util'
                description = 'A library of utilities for parsing text that contains ANSI control sequences'
                url = 'https://github.com/gradle/ansi-control-sequence-util'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'adam'
                        name = 'Adam Murdoch'
                    }
                    developer {
                        name = 'The Gradle team'
                        organization = 'Gradle Inc.'
                        organizationUrl = 'https://gradle.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/gradle/ansi-control-sequence-util.git'
                    url = 'https://github.com/gradle/ansi-control-sequence-util'
                }
            }
            from components.java
        }
    }
}

signing {
    sign publishing.publications.maven
    useInMemoryPgpKeys(System.getenv("PGP_SIGNING_KEY"), System.getenv("PGP_SIGNING_KEY_PASSPHRASE"))
}
